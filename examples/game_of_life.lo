type World = [[bool; 18]; 9];

main {
    let world: World = gen_random_world();

    while true {
        game_loop(&world);
        wait_s((1 / 8));
    }
}

func game_loop(world: &World) {
    print_world(world);
    *world = gen_next_world(world);
}

func gen_random_world() -> World {
    let world: World = [[false...]...];

    let x: uint = 0;
    while (x < 18) {
        let y: uint = 0;
        while (y < 9) {
            let rand: num = 0;
            random_num(&rand, 0, 1);
            world[y][x] = (rand < 0.33);
            y = (y + 1);
        }

        x = (x + 1);
    }

    world
}

func print_world(world: &World) {
    let y: uint = 0;
    while (y < 9) {
        let x: uint = 0;
        let row: str = "";

        while (x < 18) {
            row = (row ~ if (*world)[y][x] { "██" } else { "░░" });
            x = (x + 1);
        }

        stdout_write(row, y);
        y = (y + 1);
    }
}

func gen_next_world(curr: &World) -> World {
    let next: World = [[false...]...];

    let x: uint = 0;
    while (x < 18) {
        let y: uint = 0;
        while (y < 9) {
            let neighbors: uint = count_neighbors(curr, x, y);
            next[y][x] = ((neighbors == 3) || ((*curr)[y][x] && (neighbors == 2)));

            y = (y + 1);
        }

        x = (x + 1);
    }

    next
}

func count_neighbors(world: &World, x: uint, y: uint) -> uint {
    let count: uint = 0;

    let dx: int = -1;
    while (dx <= 1) {
        let nx: uint = (x + <<uint>>dx);
        if (nx < 0) {
            nx = (18 + nx);
        } else if (nx >= 18) {
            nx = <<uint>>(nx - 18);
        }

        let dy: int = -1;
        while (dy <= 1) {
            let ny: uint = (y + <<uint>>dy);
            if (ny < 0) {
                ny = (9 + ny);
            } else if (ny >= 9) {
                ny = <<uint>>(ny - 9);
            }

            if (!((x == nx) && (y == ny))) {
                if (*world)[ny][nx] {
                    count = (count + 1);
                }
            }

            dy = (dy + 1);
        }

        dx = (dx + 1);
    }

    count
}
