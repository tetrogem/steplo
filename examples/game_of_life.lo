main {
    let cells: [[bool; 18]; 9] = [[false...]...];
    gen_random_cells(&cells);

    while true {
        game_loop(&cells);
        wait_s((1 / 8));
    }
}

func game_loop(cells: &[[bool; 18]; 9]) {
    print_cells(cells);
    let next: [[bool; 18]; 9] = [[false...]...];
    gen_next_step(&next, cells);
    *cells = next;
}

func gen_random_cells(return: &[[bool; 18]; 9]) {
    let x: uint = 0;
    while (x < 18) {
        let y: uint = 0;
        while (y < 9) {
            let rand: num = 0;
            random_num(&rand, 0, 1);
            (*return)[y][x] = (rand < 0.33);
            y = (y + 1);
        }

        x = (x + 1);
    }
}

func print_cells(cells: &[[bool; 18]; 9]) {
    let y: uint = 0;
    while (y < 9) {
        let x: uint = 0;
        let row: str = "";

        while (x < 18) {
            row = (row ~ if (*cells)[y][x] { "██" } else { "░░" });
            x = (x + 1);
        }

        stdout_write(row, y);
        y = (y + 1);
    }
}

func gen_next_step(return: &[[bool; 18]; 9], curr: &[[bool; 18]; 9]) {
    let x: uint = 0;
    while (x < 18) {
        let y: uint = 0;
        while (y < 9) {
            let neighbors: uint = 0;
            count_neighbors(&neighbors, curr, x, y);
            (*return)[y][x] = ((neighbors == 3) || ((*curr)[y][x] && (neighbors == 2)));

            y = (y + 1);
        }

        x = (x + 1);
    }
}

func count_neighbors(return: &uint, cells: &[[bool; 18]; 9], x: uint, y: uint) {
    *return = 0;

    let dx: int = -1;
    while (dx <= 1) {
        let nx: uint = (x + <<uint>>dx);
        if (nx < 0) {
            nx = (18 + nx);
        } else if (nx >= 18) {
            nx = <<uint>>(nx - 18);
        }

        let dy: int = -1;
        while (dy <= 1) {
            let ny: uint = (y + <<uint>>dy);
            if (ny < 0) {
                ny = (9 + ny);
            } else if (ny >= 9) {
                ny = <<uint>>(ny - 9);
            }

            if (!((x == nx) && (y == ny))) {
                if (*cells)[ny][nx] {
                    *return = (*return + 1);
                }
            }

            dy = (dy + 1);
        }

        dx = (dx + 1);
    }
}
