main {
    let arr: [val; 99] = [0, 10, "a", "-5", 283, 2, 6, 4, "what", "-1", 20, 7, 8, 2, 0...];
    let vec: Vec = Vec_new(<<&any>>&arr, 99, 14);

    out("Unsorted:");
    let str: str = "";
    out(Vec_to_str(&vec));

    out("Sorted:");
    Vec_sort(&vec);
    out(Vec_to_str(&vec));

    out("Pushing `12.5` then sorting again:");
    Vec_push(&vec, "12.5");
    Vec_sort(&vec);
    out(Vec_to_str(&vec));
}

type Vec = {
    arr: &any,
    cap: uint,
    len: uint,
};

func Vec_new(arr: &any, cap: uint, len: uint) -> Vec { arr: arr, cap: cap, len: len }
func Vec_get_mut(self: &Vec, i: uint) -> &any <<&any>>(<uint>(*self).arr + i)
func Vec_get(self: &Vec, i: uint) -> any *Vec_get_mut(self, i)

func Vec_push(self: &Vec, val: any) {
    let entry: &any = Vec_get_mut(self, (*self).len);
    *entry = val;
    (*self).len = ((*self).len + 1);
}

func Vec_pop(self: &Vec) -> any {
    let val: any = Vec_get(self, <<uint>>((*self).len - 1));
    (*self).len = <<uint>>((*self).len - 1);
    val
}

func Vec_insert(self: &Vec, val: any, index: uint) {
    let i: uint = (*self).len;
    Vec_push(self, 0);

    while (i >= index) {
        let entry: &any = Vec_get_mut(self, i);
        let next_entry: &any = Vec_get_mut(self, (i + 1));
        *next_entry = *entry;

        i = <<uint>>(i - 1);
    }

    let entry: &any = Vec_get_mut(self, index);
    *entry = val;
}

func Vec_remove(self: &Vec, index: uint) -> any {
    let ret: any = Vec_get(self, index);
    let i: uint = index;
    (*self).len = <<uint>>((*self).len - 1);

    while (i < (*self).len) {
        let entry: &any = Vec_get_mut(self, i);
        let next_entry: &any = Vec_get_mut(self, (i + 1));
        *entry = *next_entry;

        i = (i + 1);
    }

    ret
}

func Vec_clear(self: &Vec) {
    (*self).len = 0;
}

func Vec_to_str(self: &Vec) -> str {
    let str: str = "[";
    let i: uint = 0;

    while (i < (*self).len) {
        let val: val = <<val>>Vec_get(self, i);
        str = (str ~ val);
        if (i != ((*self).len - 1)) {
            str = (str ~ ", ");
        }

        i = (i + 1);
    }

    str = (str ~ "]");
    str
}

func Vec_sort(self: &Vec) {
    let i: uint = 0;
    while (i < (*self).len) {
        let j: uint = i;
        while (j < (*self).len) {
            let a_r: &val = <<&val>>Vec_get_mut(self, i);;
            let b_r: &val = <<&val>>Vec_get_mut(self, j);;

            let a: val = *a_r;
            let b: val = *b_r;

            if (<<num>>b < <<num>>a) {
                swap(a_r, b_r);
            }

            j = (j + 1);
        }

        i = (i + 1);
    }
}

func swap(a_r: &val, b_r: &val) {
    let temp: val = *a_r;
    *a_r = *b_r;
    *b_r = temp;
}

// Expected stdout:
// Unsorted:
// [0, 10, a, -5, 283, 2, 6, 4, what, -1, 20, 7, 8, 2]
// Sorted:
// [-5, -1, 0, 2, 2, 4, 6, 7, 8, 10, 20, 283, a, what]
// Pushing `12.5` then sorting again:
// [-5, -1, 0, 2, 2, 4, 6, 7, 8, 10, 12.5, 20, 283, a, what]
