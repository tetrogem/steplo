main {
    let arr: [val; 99] = [0, 10, "a", "-5", 283, 2, 6, 4, "what", "-1", 20, 7, 8, 2, 0...];
    let vec: Vec = undefined;
    Vec_new(&vec, <<&any>>&arr, 99, 14);

    out("Unsorted:");
    let str: str = "";
    Vec_to_str(&str, &vec);
    out(str);

    out("Sorted:");
    Vec_sort(&vec);
    Vec_to_str(&str, &vec);
    out(str);

    out("Pushing `12.5` then sorting again:");
    Vec_push(&vec, "12.5");
    Vec_sort(&vec);
    Vec_to_str(&str, &vec);
    out(str);
}

type Vec = {
    arr: &any,
    cap: uint,
    len: uint,
};

func Vec_new(ret: &Vec, arr: &any, cap: uint, len: uint) {
    *ret = { arr: arr, cap: cap, len: len };
}

func Vec_get_mut(ret: &&any, self: &Vec, i: uint) {
    *ret = <<&any>>(<uint>(*self).arr + i);
}

func Vec_get(ret: &any, self: &Vec, i: uint) {
    let entry: &any = undefined;
    Vec_get_mut(&entry, self, i);
    *ret = *entry;
}

func Vec_push(self: &Vec, val: any) {
    let entry: &any = undefined;
    Vec_get_mut(&entry, self, (*self).len);
    *entry = val;
    (*self).len = ((*self).len + 1);
}

func Vec_pop(ret: &any, self: &Vec) {
    let val: any = 0;
    Vec_get(&val, self, <<uint>>((*self).len - 1));
    *ret = val;
    (*self).len = <<uint>>((*self).len - 1);
}

func Vec_insert(self: &Vec, val: any, index: uint) {
    let i: uint = (*self).len;
    Vec_push(self, 0);

    let entry: &any = undefined;
    while (i >= index) {
        Vec_get_mut(&entry, self, i);

        let next_entry: &any = undefined;
        Vec_get_mut(&next_entry, self, (i + 1));
        *next_entry = *entry;

        i = <<uint>>(i - 1);
    }

    Vec_get_mut(&entry, self, index);
    *entry = val;
}

func Vec_remove(ret: &any, self: &Vec, index: uint) {
    Vec_get(ret, self, index);
    let i: uint = index;
    (*self).len = <<uint>>((*self).len - 1);

    while (i < (*self).len) {
        let entry: &any = undefined;
        Vec_get_mut(&entry, self, i);

        let next_entry: &any = undefined;
        Vec_get_mut(&next_entry, self, (i + 1));
        *entry = *next_entry;

        i = (i + 1);
    }
}

func Vec_clear(self: &Vec) {
    (*self).len = 0;
}

func Vec_to_str(ret: &str, self: &Vec) {
    let str: str = "[";
    let i: uint = 0;

    while (i < (*self).len) {
        let val: val = 0;
        Vec_get(<<&any>>&val, self, i);
        str = (str ~ val);
        if (i != ((*self).len - 1)) {
            str = (str ~ ", ");
        }

        i = (i + 1);
    }

    str = (str ~ "]");
    *ret = str;
}

func Vec_sort(self: &Vec) {
    let i: uint = 0;
    while (i < (*self).len) {
        let j: uint = i;
        while (j < (*self).len) {
            let a_r: &val = undefined;
            let b_r: &val = undefined;
            Vec_get_mut(<<&&any>>&a_r, self, i);
            Vec_get_mut(<<&&any>>&b_r, self, j);
            let a: val = *a_r;
            let b: val = *b_r;

            if (<<num>>b < <<num>>a) {
                swap(a_r, b_r);
            }

            j = (j + 1);
        }

        i = (i + 1);
    }
}

func swap(a_r: &val, b_r: &val) {
    let temp: val = *a_r;
    *a_r = *b_r;
    *b_r = temp;
}
