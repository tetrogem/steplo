type Vec = {
    arr: &any,
    cap: uint,
    len: uint,
};

main {
    let arr: [any; 10] = undefined;
    let vec: Vec = Vec_new(<<&any>>&arr, 10);

    out(Vec_to_str(&vec));

    Vec_push(&vec, 10);
    Vec_push(&vec, 2);
    Vec_push(&vec, 6);
    out(Vec_to_str(&vec));

    let temp: val = <<val>>Vec_pop(&vec);
    out(("Popped: " ~ temp));
    out(Vec_to_str(&vec));

    Vec_insert(&vec, 99, 1);
    out(Vec_to_str(&vec));

    temp = <<val>>Vec_remove(&vec, 0);
    out(("Removed: " ~ temp));
    out(Vec_to_str(&vec));

    Vec_insert(&vec, 123, 0);
    out(Vec_to_str(&vec));

    Vec_insert(&vec, 456, 0);
    out(Vec_to_str(&vec));

    Vec_insert(&vec, -1, 2);
    out(Vec_to_str(&vec));

    Vec_clear(&vec);
    out(Vec_to_str(&vec));

    Vec_insert(&vec, 1, 0);
    out(Vec_to_str(&vec));
}

func Vec_new(arr: &any, cap: uint) -> Vec {
    { arr: arr, cap: cap, len: 0 }
}

func Vec_get_mut(self: &Vec, i: uint) -> &any {
    <<&any>>(<uint>(*self).arr + i)
}

func Vec_get(self: &Vec, i: uint) -> any {
    *Vec_get_mut(self, i)
}

func Vec_push(self: &Vec, val: any) {
    let entry: &any = Vec_get_mut(self, (*self).len);
    *entry = val;
    (*self).len = ((*self).len + 1);
}

func Vec_pop(self: &Vec) -> any {
    let val: any = Vec_get(self, <<uint>>((*self).len - 1));
    (*self).len = <<uint>>((*self).len - 1);
    val
}

func Vec_insert(self: &Vec, val: any, index: uint) {
    let i: uint = (*self).len;
    Vec_push(self, 0);

    while (i >= index) {
        *Vec_get_mut(self, (i + 1)) = *Vec_get_mut(self, i);
        i = <<uint>>(i - 1);
    }

    *Vec_get_mut(self, index) = val;
}

func Vec_remove(self: &Vec, index: uint) -> any {
    let val: any = Vec_get(self, index);
    let i: uint = index;
    (*self).len = <<uint>>((*self).len - 1);

    while (i < (*self).len) {
        *Vec_get_mut(self, i) = *Vec_get_mut(self, (i + 1));
        i = (i + 1);
    }

    val
}

func Vec_clear(self: &Vec) {
    (*self).len = 0;
}

func Vec_to_str(self: &Vec) -> str {
    let str: str = "[";
    let i: uint = 0;

    while (i < (*self).len) {
        let val: val = <<val>>Vec_get(self, i);
        str = (str ~ val);
        if (i != ((*self).len - 1)) {
            str = (str ~ ", ");
        }

        i = (i + 1);
    }

    (str ~ "]")
}

// Expected stdout:
// []
// [10, 2, 6]
// Popped: 6
// [10, 2]
// [10, 99, 2]
// Removed: 10
// [99, 2]
// [123, 99, 2]
// [456, 123, 99, 2]
// [456, 123, -1, 99, 2]
// []
// [1]
